apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.1.7.18"

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 2
        versionName "1.1.7.18"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

def siteUrl = 'https://github.com/AdyoOrg/adyo-android'      // Homepage URL of the library
def gitUrl = 'https://github.com/AdyoOrg/adyo-android.git'   // Git repository URL
group = "za.co.adyo"                                        // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {

        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Adyo Android SDK'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT'
                    }
                }
                developers {
                    developer {
                        id 'marilie.moolman'
                        name 'marilie'
                        email 'marilie@unitx.co.za'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    api 'com.google.android.gms:play-services-ads:15.0.1'
    implementation 'com.android.support:customtabs:26.0.2'

}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}


artifacts {
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "adyo-android-sdk"
        name = "adyo-android-sdk"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}
